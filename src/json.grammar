@top JsonText { value }

value { True | False | Null | Number | String | Object | Array | TemplateVar }

String { string }
Object { "{" list<Property>? "}" }
Array  { "[" list<value>? "]" }

Property { PropertyName ":" value }
PropertyName { string | TemplateVar }


@tokens {
  True  { "true" }
  False { "false" }
  Null  { "null" }
  TemplateVar { "{{" char+ "}}" }

  Number { '-'? int frac? exp?  }
  int  { '0' | $[1-9] std.digit* }
  frac { '.' std.digit+ }
  exp  { $[eE] $[+\-]? std.digit+ }

  string { '"' (char | "{" )* '"' }
  char { $[\u{20}\u{21}\u{23}-\u{5b}\u{5d}-\u{7a}\u{7c}-\u{10ffff}] | "\\" esc }

  esc  { $["\\\/bfnrt] | "u" hex hex hex hex }
  hex  { $[0-9a-fA-F] }

  whitespace { $[ \n\r\t] }

  "{{" "}}"

  "{" "}" "[" "]"

  @precedence {
    TemplateVar "{"
  }
}

@skip { whitespace }
list<item> { item ("," item)* }

@detectDelim
